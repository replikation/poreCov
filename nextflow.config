manifest {
    mainScript = 'poreCov.nf'
    name = 'poreCov'
    nextflowVersion = '!>=24.10.4'
}

// default parameters
params {
    max_cores = Runtime.runtime.availableProcessors()
    cores = Runtime.runtime.availableProcessors().intdiv(4)
    memory = '12'
    help = false
    profile = false
    trace_timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')

    // inputs
    fast5 = ''
    fasta = ''
    multifasta = ''
    fastq = ''
    fastq_pass = ''
    samples = false

    artic_normalize = 500

    // consensus qc
    reference_for_qc = ''
    seq_threshold = '0.90'
    n_threshold = '0.05'
    min_depth = '20'

    // settings
    buildDB = false
    cloudProcess = false
    extended = false
    freyja = false
    freyja_update = false
    guppy_cpu = false
    guppy_model = 'dna_r9.4.1_450bps_hac.cfg'
    krakendb = ''
    lcs = false
    localguppy = false
    clair3_model_name = 'r941_prom_sup_g5014'
    // --model-dir defaults to CONDA_ROOT_PREFIX but somehow this is not set in seqera containers and instead points to MAMBA_ROOT... 
    clair3_model_dir = '/opt/conda/bin/models/'
    one_end = false
    single = false
    update = false
    screen_reads = false
    lcs_ucsc_version = 'predefined' // e.g. '2022-05-01' marker-table date
    lcs_ucsc_predefined = '2023-01-26'
    lcs_ucsc_update = false // update marker table, overrides lcs_ucsc_version
    lcs_ucsc_downsampling = 10000 // 'None' to turn off
    lcs_variant_groups = 'default' // 'default' to use file from repo; custom variant groups table for marker-table update
    lcs_cutoff = 0.03
    defaultpangolin = 'nanozoo/pangolin-v4:4.3--1.25.1'
    defaultnextclade = 'nanozoo/nextclade3:3.2.1--2024-02-16'
    scorpio = false

    // parameters
    primerV = 'v1.0.0'
    primerRef = ''
    schemeLength = 400
    minLength = false
    maxLength = false
    rapid = false

    // folder structure
    cachedir = "singularity_images"
    databases = "databases"

    output = 'results'
    readsdir = "0.Fastq_reads"
    readqcdir = "1.Read_quality"
    genomedir = "2.Genomes"
    lineagedir = "3.Lineages_Clades_Mutations"
    rkidir = "4.RKI-summary"
    seqrepair = "5.Genome-primer-repair"
    jsondir = "6.json-summaries"
    runinfodir = "X.Pipeline-runinfo"

    // deprecated params
    nanopolish = false
    medaka_model = false
}

// runinfo
// def trace_timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')
timeline {
  enabled = true
  file = "${params.output}/${params.runinfodir}/execution_timeline_${params.trace_timestamp}.html"
}

report {
  enabled = true
  file = "${params.output}/${params.runinfodir}/execution_report_${params.trace_timestamp}.html"
}

includeConfig ({
    if (params.localguppy)
        return 'configs/localguppy.config'
    else
        return '/dev/null'
}())

profiles {

    test_fastq {
        params.primerV = 'v1.0.0'
    }

    stub {
        params.primerV = 'v1.0.0'
    }

    test_fasta {
    }

    test_fast5 {
        params.single = true
    }


    local {
        workDir = "work/nextflow-poreCov-$USER"
        includeConfig 'configs/local.config'
        executor {
            name = "local"
            cpus = params.max_cores
            memory = params.memory
        }

        //process.errorStrategy = 'ignore'
    }

    slurm {
        includeConfig 'configs/nodes.config'
        executor {
            name = "slurm"
            queueSize = 100
            // exitReadTimeout = "360 sec"
            // submitRateLimit = '1 / 2 s'
            // pollInterval = '30 sec'
        }        
        process.cache = "lenient"


        process.errorStrategy = 'retry'
        process.maxRetries = 1
    }

    docker {
        docker { enabled = true }
        includeConfig 'configs/container.config'
    }

    singularity {
        singularity { 
                enabled = true
                autoMounts = true
                cacheDir = params.cachedir
                envWhitelist = "HTTPS_PROXY,HTTP_PROXY,http_proxy,https_proxy,FTP_PROXY,ftp_proxy"
                //runOptions = "-B /tmp/nextflow-nCov-$USER"
        }
        includeConfig 'configs/container.config'
    }
    

    ukj_cloud { 
        workDir = 'gs://case-tmp-dir/poreCov/'
        docker { enabled = true }
        process {
            executor = 'google-batch'
            errorStrategy = { task.exitStatus in [10,14,143,137,104,134,139] ? 'retry' : 'ignore' }
            maxRetries = 3
        }
        params {
            databases = 'gs://databases-case-grp/poreCov/'
        }
        google {  
            project = 'case-dev-302214'
            location = 'europe-west4'
            batch { 
                spot = true
                bootDiskSize = 20.GB
                maxSpotAttempts = 4
            }
        }
        includeConfig 'configs/container.config'
        includeConfig 'configs/nodes.config'
    }
}
